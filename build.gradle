// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        agpVersion = "4.1.3"
        kotlinVersion = "1.4.32"
        gdxVersion = "1.10.0"
        ktxVersion = '1.10.0-b1'
    }

    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$agpVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

ext {
    appName = "CatBreeder"
}

allprojects {
    version "0.0.0"

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
}

project(':util') {
    apply plugin: 'java-library'
    apply plugin: 'kotlin'

    compileKotlin.kotlinOptions.useIR = true

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
}

project(':core') {
    apply plugin: 'java-library'
    apply plugin: 'kotlin'

    compileKotlin.kotlinOptions.useIR = true

    dependencies {
        api project(':util')

        implementation "io.github.libktx:ktx-scene2d:$ktxVersion"
        implementation 'org.nield:kotlin-statistics:1.2.1'
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        api "com.badlogicgames.gdx:gdx:$gdxVersion"

        testImplementation 'junit:junit:4.12'
    }
}

project(':desktop') {
    apply plugin: 'java-library'
    apply plugin: 'kotlin'

    compileKotlin.kotlinOptions.useIR = true

    dependencies {
        implementation project(':core')
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        api "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(':android') {
    apply plugin: 'com.android.application'
    apply plugin: 'kotlin-android'

    android {
        compileSdkVersion 30

        defaultConfig {
            applicationId "com.lantopia.catbreeder"

            minSdkVersion 19
            targetSdkVersion 30

            versionCode 1
            versionName project.version

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = '1.8'
            useIR = true
        }
    }

    configurations { natives }

    dependencies {
        implementation project(':core')

        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        implementation 'androidx.core:core-ktx:1.3.2'
        implementation 'androidx.appcompat:appcompat:1.2.0'
        implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

        api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"

        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
    }
}
